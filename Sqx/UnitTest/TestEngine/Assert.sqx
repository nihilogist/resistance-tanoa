/*
 * Name:	Assert
 * Date:	2018-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Asserts different kinds of checks.
 */
namespace Sqx.UnitTest
{
	public class Assert
	{
		public static method AreEqual("_expected" as Any, "_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			if (!(_expected == _actual)) then {
				_fullMsg = "Expected '" + str _expected + "' but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method AreNotEqual("_expected" as Any, "_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			if (_expected == _actual) then {
				_fullMsg = "Expected '" + str _expected + "' but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsTrue("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			if (!_actual) then {
				_fullMsg = "Expected 'true' but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsFalse("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];

			if (_actual) then {
				_fullMsg = "Expected 'false' but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		private static method ValueIsNull("_value" as Any)
		{
			private ["_valueSqxClass" as Assert, "_isNull" as Boolean];

			_valueSqxClass = _value as Assert;
			_isNull = false;
			
			if (isNull _valueSqxClass || { isNull _value }) then {
				_isNull = true;
			};
			
			_isNull
		};
		
		public static method IsNull("_actual" as Any, ["_message" as String, ""])
		{
			private ["_fullMsg" as String];
			
			if (!([_actual] call _self.ValueIsNull)) then {
				_fullMsg = "Expected NULL but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
		
		public static method IsNotNull("_actual" as Any, ["_message" as String, ""]) {
			private ["_fullMsg" as String];

			if ([_actual] call _self.ValueIsNull) then {
				_fullMsg = "Expected NOT NULL but got '" + str _actual + "'. ";
				
				if (_message != "") then {
					_fullMsg = _fullMsg + _message;
				};
			
				[_fullMsg] call TestEngine.WriteFailed;
			};
		};
	};
};
