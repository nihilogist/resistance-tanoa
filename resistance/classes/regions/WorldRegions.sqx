/*
 * Name:	World
 * Date:	04/08/2019
 * Version: 1.0
 * Author:  dave3
 *
 * Description:
 * Represents the whole world (island)
 */
namespace dave3
{
	public class WorldRegions : ISaveable
	{
		
		public property String worldName { get; private set; };
		
		public property Array regions { get; private set; };
		
		// Creates a World object.
		public constructor {
			params ["_worldData" as Array];

			private ["_regions" as Array, "_region" as Region, "_regionName" as String, "_regionMilitarisation" as Scalar, "_regionPrisonCampMarkers" as Array, "_regionGendarmeHQMarker" as String, "_regionTowns" as Array];

			_self.worldName = _worldData select 0;
			_regions = _worldData select 1;
			
			[["Initialising World ", _self.worldName] joinString ""] call logger;
			[["Initialising ", (count _regions), " regions"] joinString ""] call logger;
			
			_self.regions = [];
			// Iterate over all regions and create them
			{
				_regionName = _x select 0;
				_regionMilitarisation = _x select 1;
				_regionPrisonCampMarkers = _x select 2;
				_regionGendarmeHQMarker = _x select 3;
				_regionTowns = _x select 4;
				_region = [_regionName, _regionMilitarisation, _regionPrisonCampMarkers, _regionGendarmeHQMarker, _regionTowns] new Region;
				_self.regions pushBack _region;
			} forEach _regions;
		};
		
		
		public method Array getSaveableData {
			private ["_saveData" as Array, "_saveableRegions" as Array, "_saveableRegion" as Array];
			
			_saveableRegions = [];
			
			{
				_saveableRegion = [] call _x.getSaveableData;
				_saveableRegions pushBack _saveableRegion;
			} forEach _self.regions as Region;
			
			_saveData = [
				_self.worldName,
				_saveableRegions
			];
			
			_saveData
		};
	};
};

