/*
 * Name:	PrisonCamp
 * Date:	03/08/2019
 * Version: 1.0
 * Author:  dave3
 *
 * Description:
 * Object representing a prison camp
 */
namespace dave3
{
	public class PrisonCamp : ISaveable
	{
	
		public property String locationMarker { get; private set; };
	
		// Creates a PrisonCamp object and spawns the prison camp buildings at the given location
		public constructor {
			params ["_prisonCampData" as Array];
			private ["_prisonCampShedClass" as String, "_prisonCampGateClass" as String, "_prisonCampSidewallClass" as String, "_positionToSpawn", "_prisonCampShed"];
			
			// Deserialise data from array
			_self.locationMarker = _prisonCampData select 0;

			// Define buildings used in constructing the camp.
			_prisonCampShedClass = "Land_Metal_Shed_F";
			_prisonCampGateClass = "Land_Net_Fence_Gate_F";
			_prisonCampSidewallClass = "Land_BackAlley_02_l_1m_F";

			// Get the position to spawn the camp.
			_positionToSpawn = markerPos _self.locationMarker;
			
			// Spawn in the buildings and arrange them.
			_prisonCampShed = [_prisonCampShedClass, _self.locationMarker] call spawnObjectAtMarker;
			
			
			private _prisonCampWall1 = _prisonCampSidewallClass createVehicle (_positionToSpawn);
			_prisonCampWall1 attachTo [_prisonCampShed, [5.2, -1.8, 0]];
			_prisonCampWall1 setVectorDirAndUp [[-1,0,0],[0,0,1]];
			
			
			private _prisonCampWall2 = _prisonCampSidewallClass createVehicle (_positionToSpawn);
			_prisonCampWall2 attachTo [_prisonCampShed, [-5.2, -1.8, 0]];
			_prisonCampWall2 setVectorDirAndUp [[1,0,0],[0,0,1]];
			
			private _prisonCampGate = _prisonCampGateClass createVehicle (_positionToSpawn);
			_prisonCampGate attachTo [_prisonCampShed, [0, 0.8, 0]];
			_prisonCampGate setVectorDirAndUp [[0,-1,0], [0,0,1]];
			
			
		};
		
		public method Array getSaveableData() {
			private ["_prisonCampData" as Array];
			
			_prisonCampData = [_self.locationMarker];
			
			_prisonCampData
			
		};
	};
	
};

